allprojects {
    apply plugin: 'java'

    group 'cc.koosha'
    version '1.0-SNAPSHOT'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
        mavenLocal()
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

subprojects {
    compileJava {
        options.compilerArgs << '-Xlint:unchecked'
    }

    test {
        useTestNG()
    }

    dependencies {
        compile 'org.projectlombok:lombok:1.16.8'

        testCompile 'org.testng:testng:6.9.10'
        testCompile 'org.slf4j:slf4j-api:1.7.21'
        testCompile 'org.slf4j:slf4j-simple:1.7.21'
    }
}

task allJar(type: Jar, dependsOn: subprojects.tasks["build"]) {
    baseName = 'allJar'
    subprojects.each { subproject ->
        from subproject.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

artifacts {
    archives allJar
}

